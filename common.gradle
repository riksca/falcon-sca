//
// This file is to be applied to every subproject.
//
apply plugin: 'java'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    jcenter()
    mavenCentral();
}

ext {
    appengineSdkVersion = '1.9.62'
    appenginePluginVersion = '1.9.59'
}

dependencies {
    // Adding dependencies here will add the dependencies to each subproject.

    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'javax.servlet.jsp', name: 'jsp-api', version: '2.2'
    compile group: 'jstl', name: 'jstl', version: '1.2'
    compile group : 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group : 'joda-time', name: 'joda-time', version: '2.9.7'
    // Many of these will be moved to subprojects as they are broken up
    // tests
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}
