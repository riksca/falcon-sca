//
// This file is to be applied to every subproject.
//
apply plugin: 'java'

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral();
}

ext {
    appengineSdkVersion = '1.9.25'
    appenginePluginVersion = '1.9.24'
    gwtVersion = '2.7.0'
}

dependencies {
    // Adding dependencies here will add the dependencies to each subproject.

    compile "com.google.appengine:appengine-api-1.0-sdk:${appengineSdkVersion}"
    compile 'javax.servlet:servlet-api:2.5'
    compile 'javax.servlet:jsp-api:2.0'
    compile 'jstl:jstl:1.2'
    compile group : 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group : 'joda-time', name: 'joda-time', version: '2.8.2'
    // Many of these will be moved to subprojects as they are broken up
    // tests
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile 'com.google.appengine:appengine-testing:1.8.5'
    testCompile 'com.google.appengine:appengine-api-stubs:1.8.5'
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}
